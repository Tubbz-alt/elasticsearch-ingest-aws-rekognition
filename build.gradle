buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath "org.elasticsearch.gradle:build-tools:${elasticsearch_version}"
    }
}

group 'com.brusic.elasticsearch.plugin.ingest'
version '1.0'

apply plugin: 'elasticsearch.esplugin'
apply plugin: 'idea'

esplugin {
    name 'ingest-aws-rekognition'
    description 'Ingest processor that uses AWS Rekognition for image analysis'
    classname 'com.brusic.elasticsearch.ingest.rekognition.IngestAwsRekognitionPlugin'
}

dependencies {
    compile group: 'com.amazonaws', name: 'aws-java-sdk-rekognition', version: "${aws_version}"

    // Elasticsearch removes transitive dependencies
    // and forces this behavior on plugin authors WTF ^:-/
    compile group: 'com.amazonaws', name: 'aws-java-sdk-core', version: "${aws_version}"
    compile group: 'commons-logging', name: 'commons-logging', version: '1.1.3'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.6.7'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.6.7.1'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.2'
    compile group: 'org.apache.httpcomponents', name: 'httpcore', version: '4.4.5'
    compile group: 'joda-time', name: 'joda-time', version: '2.9.5'
}

licenseFile = rootProject.file('LICENSE')
noticeFile = rootProject.file('NOTICE')
project.ext.projectSubstitutions = ["": ""] // seems like a bug

dependencyLicenses.enabled = false
thirdPartyAudit.enabled = false
licenseHeaders.enabled = false

forbiddenPatterns {
    exclude '**/*.jpg'
}

integTestCluster {
    // if these values are defined, integration tests will be run against the actual API
//    keystoreSetting 'ingest.aws-rekognition.credentials.access_key', 'myaccesskey'
//    keystoreSetting 'ingest.aws-rekognition.credentials.secret_key', 'mysecretkey'
}

integTestRunner.systemProperty 'tests.rest.suite', 'integtest'

project.afterEvaluate {
    if (!integTestCluster.keystoreSettings.containsKey("ingest.aws-rekognition.credentials.access_key")
            || !integTestCluster.keystoreSettings.containsKey("ingest.aws-rekognition.credentials.secret_key")) {
        // skip most integration tests
        integTestRunner.systemProperty 'tests.rest.suite', 'no_processors'
    }
}